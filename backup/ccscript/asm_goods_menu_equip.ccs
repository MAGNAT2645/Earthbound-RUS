// Изначально создал Chaz для EarthBound Randomizer
// Адаптация под CCScript от PhoenixBound, ShadowOne333, и vince94
// Патч позволяет экипировать предметы прямо из меню Вещи!

command e(label) "{long label}"
define UsingEquipmentCheckFlag = flag 0x3F0
ROM[0xC7C742] = goto(newUse)
ROM[0xC5E36E] = goto(newCheck)
ROM[0xC5E277] = goto(equipMessagePartOne)
ROM[0xC5E2F7] = goto(equipMessagePartTwo)

/////////////////////////////////////////////////////////////
// Изменения, касающиеся того, что произойдёт, когда вы Используете экипируемый предмет //
newUse:
	store_registers
	if not "[1D 11 00 00]" // Предмет можно экипировать?
//		"@{user} не может экипировать [19 1F]{swap}{itemname(0)}." end
		"@{user} [9F][96] [9E][A0][98][96][A4] [C4][9C][9A][A1][9A][A2][A0][93][91][A4][C3] [19 1F]{swap}{itemname(0)}." end

	"[19 1F]"
	swap
	call(0xC5E25B)

	load_registers
	set(UsingEquipmentCheckFlag)
	goto(0xC5E136)

newCheck:
	swap
	store_registers

	if isset(UsingEquipmentCheckFlag)
	{
		//loadZero
		unset(UsingEquipmentCheckFlag)
		isset(UsingEquipmentCheckFlag)
		eob
	}

	load_registers
	"[19 19 00 00][1D 0B 00]"
	eob

//////////////////////////////////
// Изменения к сообщению об экипировке //
equipMessagePartOne:
	load_registers
	rtoarg
	"@([1C 02 00]: "
	"[06 92 02 {e(initialOffense)}]"
	goto(initialDefense)

initialDefense:
	/*
	"Защита" linebreak
	"  изменилась с [09 04 {e(nessDefenseStat)} {e(paulaDefenseStat)} {e(jeffDefenseStat)} {e(pooDefenseStat)}]"
	*/
	"[79][91][C0][9A][A4][91]" linebreak
	"  [9A][99][9E][96][9F][9A][9D][91][A3][C3] [A3] [09 04 {e(nessDefenseStat)} {e(paulaDefenseStat)} {e(jeffDefenseStat)} {e(pooDefenseStat)}]"

initialOffense:
	/*
	"Атака" linebreak
	"  изменилась с [09 04 {e(nessOffenseStat)} {e(paulaOffenseStat)} {e(jeffOffenseStat)} {e(pooOffenseStat)}]"
	*/
	"[71][A4][91][9C][91]" linebreak
	"  [9A][99][9E][96][9F][9A][9D][91][A3][C3] [A3] [09 04 {e(nessOffenseStat)} {e(paulaOffenseStat)} {e(jeffOffenseStat)} {e(pooOffenseStat)}]"

equipMessagePartTwo:
	" [95][A0] " // " до "
	rtoarg
	"[06 92 02 {e(finalOffense)}]"
	goto(finalDefense)

finalDefense:
	"[1F C0 04 {e(nessDefenseStat)} {e(paulaDefenseStat)} {e(jeffDefenseStat)} {e(pooDefenseStat)}]"
	".)" wait newline eob

finalOffense:
	"[1F C0 04 {e(nessOffenseStat)} {e(paulaOffenseStat)} {e(jeffOffenseStat)} {e(pooOffenseStat)}]"
	".)" wait newline eob

///////////////////////////////////////
// Отображение статов для каждого члена команды //
nessDefenseStat:
	"[1C 01 12]" eob

paulaDefenseStat:
	"[1C 01 28]" eob

jeffDefenseStat:
	"[1C 01 3E]" eob

pooDefenseStat:
	"[1C 01 54]" eob

nessOffenseStat:
	"[1C 01 11]" eob

paulaOffenseStat:
	"[1C 01 27]" eob

jeffOffenseStat:
	"[1C 01 3D]" eob

pooOffenseStat:
	"[1C 01 53]" eob